@PACKAGE_INIT@

# This macro enables usage of find_dependency().
# https://cmake.org/cmake/help/v3.11/module/CMakeFindDependencyMacro.html
include(CMakeFindDependencyMacro)

set(nano-core-agent_COMPONENTS      coreagent
                                    transportudpv4agent
                                    transportserialagent)

if("${nano-core-agent_FIND_COMPONENTS}" STREQUAL "")
  set(nano-core-agent_FIND_COMPONENTS ${nano-core-agent_COMPONENTS})
endif()

get_filename_component(nano-core-agent_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

include(${nano-core-agent_DIR}/nano-build.cmake)

if(NOT TARGET RTIConnextDDS::c_api)
    nano_project_load_connextdds()
endif()

if(NOT TARGET nano::nanocoreagent)
    include(${nano-core-agent_DIR}/nano-core-agent-coreagent-targets.cmake)
endif()

foreach(_req_component IN LISTS nano-core-agent_FIND_COMPONENTS)
    list(FIND nano-core-agent_COMPONENTS "${_req_component}" _req_component_FOUND)
    if (_req_component_FOUND EQUAL -1)
        set(nano-core-agent_FOUND                 False)
        set(nano-core-agent_NOT_FOUND_MESSAGE
                "Unsupported nano-core-agent component: ${_req_component}")
    endif()

    if (NOT TARGET nano::nano${_req_component})
        include(${nano-core-agent_DIR}/nano-core-agent-${_req_component}-targets.cmake)
    endif()
    set(nano-core-agent_${_req_component}_FOUND           true)
endforeach()

check_required_components(nano-core-agent)



